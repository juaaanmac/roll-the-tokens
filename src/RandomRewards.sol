// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/console.sol";
import {Context} from "@openzeppelin/contracts/utils/Context.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {SignedMath} from "@openzeppelin/contracts/utils/math/SignedMath.sol";
import {VRFCoordinatorV2Interface} from "@chainlink/contracts/src/v0.8/interfaces/VRFCoordinatorV2Interface.sol";
import {VRFConsumerBaseV2} from "@chainlink/contracts/src/v0.8/vrf/VRFConsumerBaseV2.sol";
import {ChipToken} from "./ChipToken.sol";

/**
 * @title Crazy staking rewards smart contract
 * @author Juan Macri
 * @notice this smart contract generates random reward tokens
 */
contract RandomRewards is Context, VRFConsumerBaseV2 {
    using SafeERC20 for ChipToken;

    struct RequestStatus {
        bool exists;
        bool fulfilled;
        address player;
        uint256 randomResult;
    }

    // Contract Variables
    ChipToken internal immutable _chipToken;
    uint256 internal totalStaked = 0;
    int8 internal immutable RANDOM_MIN;
    int8 internal immutable RANDOM_MAX;

    // address --> balance
    mapping(address => uint256) internal balanceOf;

    // Chainlink Variables
    VRFCoordinatorV2Interface internal immutable _coordinator;
    uint64 internal immutable _subscriptionId;
    bytes32 internal constant keyHash = 0x474e34a077df58807dbe9c96d3c009b23b3c6d0cce433e59bbf5b34f823bc56c;
    uint32 internal constant callbackGasLimit = 100000;
    uint16 internal constant requestConfirmations = 3;
    uint32 internal constant numWords = 1;

    /* requestId --> requestStatus */
    mapping(uint256 => RequestStatus) public s_requests;

    modifier requestExists(uint256 _requestId) {
        require(s_requests[_requestId].exists, "nonexistent request");
        _;
    }

    // Contract events
    event DepositMade(address account, uint256 amount);

    // Chainlink events
    event RequestSent(uint256 requestId);
    event RequestFulfilled(uint256 requestId, uint256 randomResult);

    //0x8103B0A8A00be2DDC778e6e7eaa21791Cd364625
    constructor(
        uint64 subscriptionId,
        address vrfCoordinatorV2Address,
        address chipToken,
        int8 randomMin,
        int8 randomMax
    ) VRFConsumerBaseV2(vrfCoordinatorV2Address) {
        _chipToken = ChipToken(chipToken);
        _coordinator = VRFCoordinatorV2Interface(vrfCoordinatorV2Address);
        _subscriptionId = subscriptionId;
        RANDOM_MIN = randomMin;
        RANDOM_MAX = randomMax;
    }

    function deposit(uint256 amount) external {
        address sender = _msgSender();
        _chipToken.safeTransferFrom(sender, address(this), amount);
        balanceOf[sender] += amount;
        totalStaked += amount;
        emit DepositMade(sender, amount);
    }

    function roll() external returns (uint256) {
        address player = _msgSender();
        require(balanceOf[player] > 0, "no balance");

        //Calling requestRandomWords from the coordinator contract
        uint256 requestId =
            _coordinator.requestRandomWords(keyHash, _subscriptionId, requestConfirmations, callbackGasLimit, numWords);

        // store new request
        s_requests[requestId] = RequestStatus({randomResult: 0, player: player, exists: true, fulfilled: false});

        // increase allowance in ChipToken
        _chipToken.safeIncreaseAllowance(address(this), balanceOf[player]);

        emit RequestSent(requestId);

        return requestId;
    }

    function fulfillRandomWords(uint256 _requestId, uint256[] memory randomWords)
        internal
        override
        requestExists(_requestId)
    {
        // update request with random result generated by VRF
        s_requests[_requestId].fulfilled = true;
        s_requests[_requestId].randomResult = randomWords[0];

        emit RequestFulfilled(_requestId, randomWords[0]);
    }

    function withdraw(uint256 _requestId) external requestExists(_requestId) {
        address player = _msgSender();

        require(s_requests[_requestId].player == player, "invalid player");
        require(s_requests[_requestId].fulfilled, "not fulfilled");

        RequestStatus memory request = s_requests[_requestId];

        // To generate a random number between -5 and 10 inclusive
        int256 randomNumber = (int256(request.randomResult) % RANDOM_MAX) + RANDOM_MIN;

        uint256 balance = balanceOf[player];
        uint256 amount = 0;

        if (randomNumber > 0) {
            amount = balance + (balance * SignedMath.abs(randomNumber)) / 100;
        } else if (randomNumber < 0) {
            amount = balance - (balance * SignedMath.abs(randomNumber)) / 100;
        }

        _chipToken.safeTransferFrom(address(this), player, amount);

        balanceOf[player] = 0;
    }

    function getRequestStatus(uint256 _requestId)
        external
        view
        requestExists(_requestId)
        returns (bool fulfilled, uint256 randomResult)
    {
        RequestStatus memory request = s_requests[_requestId];
        return (request.fulfilled, request.randomResult);
    }
}
